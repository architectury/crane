buildscript {
    dependencies {
        classpath "me.shedaniel.crane:crane-plugin-bootstrapper:$crane_plugin_version"
    }
}

plugins {
    id "me.shedaniel.crane.crane-plugin" version "$crane_plugin_version"
    id "maven-publish"
}

group "me.shedaniel.crane"
version "$minecraft_version+build.${System.getenv("GITHUB_RUN_NUMBER") ?: "9999"}"

repositories {
    mavenCentral()
    maven { url "https://maven.shedaniel.me" }
    maven { url "https://maven.fabricmc.net" }
}

crane {
    minecraftVersion.set project.minecraft_version
}

tasks.suggestMappings {
    autoMap("net/minecraft/core/BlockPos", "pos")
    autoMap("net/minecraft/world/level/block/state/BlockState", "state")
    autoMap("net/minecraft/world/item/ItemStack", "stack")
    autoMap("net/minecraft/world/InteractionHand", "hand")
    autoMap("net/minecraft/world/phys/AABB", "box")
    autoPropose("com/mojang/brigadier/suggestion/SuggestionsBuilder", "builder")
    autoPropose("com/mojang/brigadier/context/CommandContext", "context")
    autoPropose("net/minecraft/network/FriendlyByteBuf", "buf")
}

tasks.run.apply {
    systemProperty("awt.useSystemAAFontSettings", "lcd")
}

publishing {
    publications {
        mavenCommon(MavenPublication) {
            artifact(tasks.exportTiny.output) {
                builtBy tasks.exportTiny
                classifier "tiny"
            }
            artifact(tasks.exportTinyJar.output) {
                builtBy tasks.exportTinyJar
            }
        }
    }

    repositories {
        if (System.getenv("MAVEN_PASS") != null) {
            maven {
                url = "https://deploy.shedaniel.me/"
                credentials {
                    username = "shedaniel"
                    password = System.getenv("MAVEN_PASS")
                }
            }
        }
    }
}
